CREATE OR ALTER proc CMS_Quotation_Duplicate
(  
 @CMS_QUOTATION_NO varchar(21),
 @INTERID varchar(30),
 @USERID varchar(30)
)  
AS BEGIN  
	BEGIN TRY
		begin transaction
		declare @GENNUMBER varchar(50)
		declare @temp table(GENNUMBER varchar(50))
		insert into @temp (GENNUMBER) 
		exec CMS_Quotation_GenerateNumber @INTERID
		select @GENNUMBER=GENNUMBER from @temp

		IF EXISTS(select top 1 * from CMS01131004C3 WHERE RTRIM(CMS_QUOTATION_NO)=RTRIM(@GENNUMBER))
		BEGIN
			DECLARE @MESSAGE varchar(MAX)='Document ' + @GENNUMBER + ' is exists'
			RAISERROR(@MESSAGE, 16, 1)
		END
		ELSE 
		BEGIN
			--header
			insert into CMS01131004C3(CMS_QUOTATION_NO, CMS_QUOTATION_TYPE, CMS_CONTRACT_NO, CMS_CONTRACT_TYPE, DOCDATE, CMS_SUBMITTED_DATE, From_Date, TODATE, LOCNCODE, LOCNDSCR, CURNCYID, CRNCYSYM, CUSTNMBR, CUSTNAME, 
			ADRSCODE, PYMTTYPE, PYMTRMID, PYMNTTRM, TAXTYPE, TAXSCHID, TAXDTLID, TXDTLPCT, CMS_ORDER_TYPE, CMS_KETERANGAN, CMS_Status, CRUSRID, CREATDDT, CREATETIME, MDFUSRID, MODIFDT, MODIFTIME)
			select distinct @GENNUMBER, CMS_QUOTATION_TYPE, CMS_CONTRACT_NO, CMS_CONTRACT_TYPE, DOCDATE, CMS_SUBMITTED_DATE, From_Date, TODATE, LOCNCODE, LOCNDSCR, CURNCYID, CRNCYSYM, CUSTNMBR, CUSTNAME, 
			ADRSCODE, PYMTTYPE, PYMTRMID, PYMNTTRM, TAXTYPE, TAXSCHID, TAXDTLID, TXDTLPCT, CMS_ORDER_TYPE, CMS_KETERANGAN, 1, @USERID, CAST(GETDATE() as date), CAST(GETDATE() as time),
			@USERID, CAST(GETDATE() as date), CAST(GETDATE() as time)
			from CMS01131004C3
			where RTRIM(CMS_QUOTATION_NO)=RTRIM(@CMS_QUOTATION_NO)
		
			--detail route
			insert into CMS01131005C3(CMS_QUOTATION_NO, LNITMSEQ, LNSEQNBR, CMS_ROUTEID_FROM, CMS_ROUTEID_TO, CMS_CONT_UKURAN_ID, CMS_CONT_UKURAN, CMS_KETERANGAN, 
			CMS_STUFF_LOCATION, QUANTITY, UNITPRCE, XTNDPRCE, SUBTOTAL, TAXDTLID, TXDTLPCT, TAXAMNT, TOTAL)
			select distinct @GENNUMBER, LNITMSEQ, LNSEQNBR, CMS_ROUTEID_FROM, CMS_ROUTEID_TO, CMS_CONT_UKURAN_ID, CMS_CONT_UKURAN, CMS_KETERANGAN, 
			CMS_STUFF_LOCATION, QUANTITY, UNITPRCE, XTNDPRCE, SUBTOTAL, TAXDTLID, TXDTLPCT, TAXAMNT, TOTAL
			from CMS01131005C3
			where RTRIM(CMS_QUOTATION_NO)=RTRIM(@CMS_QUOTATION_NO)
		
			--detail charges
			insert into CMS01131006C3(CMS_QUOTATION_NO, LNITMSEQ, ITEMNMBR, ITEMDESC, QUANTITY, CHRGAMNT, TAXAMNT, SUBTOTAL, TOTAL)
			select distinct @GENNUMBER, LNITMSEQ, ITEMNMBR, ITEMDESC, QUANTITY, CHRGAMNT, TAXAMNT, SUBTOTAL, TOTAL 
			from CMS01131006C3
			where RTRIM(CMS_QUOTATION_NO)=RTRIM(@CMS_QUOTATION_NO)
		END

		select top 1 GENNUMBER=@GENNUMBER
	commit
	END TRY
	BEGIN CATCH
		rollback
		DECLARE @ErrorMessage NVARCHAR(4000)
		DECLARE @ErrorSeverity INT
		DECLARE @ErrorState INT

		SELECT @ErrorMessage = ERROR_MESSAGE(),
			   @ErrorSeverity = ERROR_SEVERITY(),
			   @ErrorState = ERROR_STATE()

		RAISERROR (@ErrorMessage, @ErrorSeverity, @ErrorState)
		select @ErrorMessage as output
	END CATCH 
END  
/*  

exec CMS_Quotation_Duplicate @CMS_QUOTATION_NO=N'QUOTATION/00001',@USERID=N'teg01          '

*/
GO